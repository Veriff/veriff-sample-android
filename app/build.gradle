apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.veriff.demo"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 17
        versionName lib_version+"-"+versionCode
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        releaseSigning {
            storeFile file('signing/release.jks')
            storePassword System.getenv("VERIFF_DEMO_SIGNING_KEYSTORE_PW")
            keyAlias System.getenv("VERIFF_DEMO_SIGNING_ALIAS")
            keyPassword System.getenv("VERIFF_DEMO_SIGNING_PW")
        }
        defaultSigning {
            storeFile file('signing/debug.jks')
            storePassword 'veriff1234'
            keyAlias 'veriff'
            keyPassword 'veriff1234'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.releaseSigning
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField('String', 'ROOT_URL', "\"https://magic.veriff.me/\"")
            buildConfigField('String', 'API_CLIENT_ID', "\"1e5ac928-57c9-42da-8b06-79326f847c98\"")
            buildConfigField("String", "API_SECRET", "\"" + System.getenv("VERIFF_API_SECRET") + "\"")
        }
        debug {
            signingConfig signingConfigs.defaultSigning
            buildConfigField('String', 'ROOT_URL', "\"https://front3.staging.vrff.io/\"")
            buildConfigField('String', 'API_CLIENT_ID', "\"24d887f4-ad3e-43da-bc98-c8099ad6f430\"")
            buildConfigField("String", "API_SECRET", "\"" + System.getenv("VERIFF_API_SECRET") + "\"")
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        disable 'GradleCompatible'
    }
}

dependencies {
    implementation "com.veriff:veriff-library:$lib_version"
    implementation 'io.probity.sdk:collector:1.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'

    implementation 'me.dm7.barcodescanner:zxing:1.9.13'

    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    implementation 'org.koin:koin-android:2.0.0-GA'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

android.applicationVariants.all { variant ->
    task "installRun${variant.name.capitalize()}"(type: Exec, dependsOn: variant.installProvider, group: "run") {
        commandLine = ["adb", "shell", "monkey", "-p", variant.applicationId + " 1"]
        doLast {
            println "Launching ${variant.applicationId}"
        }
    }
}
